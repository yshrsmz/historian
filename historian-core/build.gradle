plugins {
  alias(libs.plugins.android.library)
  alias(libs.plugins.maven.publish)
  id 'jacoco'
}

// CI
def isCi = "true".equals(System.getenv("CI"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

android {
  namespace 'net.yslibrary.historian'
  compileSdk libs.versions.compileSdk.get().toInteger()

  defaultConfig {
    minSdk libs.versions.minSdk.get().toInteger()
    targetSdk libs.versions.targetSdk.get().toInteger()

    testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }

  buildTypes {
    debug {
      testCoverageEnabled true
    }
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  testOptions {
    unitTests.all {
      jacoco {
        includeNoLocationClasses = true
      }
    }
  }

  buildFeatures {
    buildConfig = false
  }
}

dependencies {
  implementation libs.annotation

  testImplementation libs.junit
  testImplementation libs.robolectric
  testImplementation libs.test.core
}

// A list of directories which should be included in coverage report
def coverageSourceDirs = ['src/main/java']
// A list of files which should be excluded from coverage report since they are generated and/or framework code
def coverageExcludeFiles = ['**/R.class', '**/R$*.class', '**/com/android/**/*.*']

jacoco {
  toolVersion = libs.versions.jacoco.get()
}

// https://issuetracker.google.com/issues/178015739
tasks.withType(Test.class).configureEach {
  jacoco {
    excludes = ['*']
    includeNoLocationClasses = true
  }
}

task jacocoTestReportDebug(
    type: JacocoReport,
    dependsOn: ['testDebugUnitTest'],
    group: 'verification'
) {
  description = "Generate Jacoco coverage reports after running tests."
  reports {
    xml.required = true
    html.required = true
    csv.required = false
  }
  getSourceDirectories().from = files(coverageSourceDirs)
  getClassDirectories().from = files(
      fileTree(
          dir: "${buildDir}/intermediates/javac/debug/classes",
          exclude: coverageExcludeFiles))
  getExecutionData().from = files("${buildDir}/jacoco/testDebugUnitTest.exec")

  doLast {
    println "jacoco xml report has been generated to file://${buildDir}/reports/jacoco/test/jacocoTestReportDebug.xml"
    println "jacoco html report has been generated to file://${reports.html.outputLocation.get().asFile}/index.html"
  }
}

mavenPublishing {
  publishToMavenCentral()

  if (project.hasProperty("signingInMemoryKey")) {
    signAllPublications()
  }

  coordinates(
    project.property("GROUP").toString(),
    project.property("POM_ARTIFACT_ID").toString(),
    project.property("VERSION_NAME").toString()
  )

  pom {
    name = project.property("POM_NAME").toString()
    description = project.property("POM_DESCRIPTION").toString()
    url = project.property("POM_URL").toString()
  }
}
